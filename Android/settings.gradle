dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        maven { url 'https://repo1.maven.org/maven2/' }
        maven { url 'https://www.jitpack.io' }
        mavenCentral()
        google()

        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/jcenter' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    }
}
def versionCtrl = new File(rootProject.projectDir.absolutePath + File.separator + "versionCtrl.properties")
def properties = new Properties()
if (versionCtrl.exists()) {
    versionCtrl.withReader('UTF-8') { reader ->
        properties.load(reader)
    }
}

final def isVoiceEmpty = properties.getProperty("VERSION_SCENE_VOICE").isEmpty()
final def isSpatialVoiceEmpty = properties.getProperty("VERSION_SCENE_SPATIAL_VOICE").isEmpty()
final def isKTVEmpty = properties.getProperty("VERSION_SCENE_KTV").isEmpty()
final def isShowEmpty = properties.getProperty("VERSION_SCENE_SHOW").isEmpty()

rootProject.name = "AgoraEntFullDemo"
include ':app'
include ':common:base'
include ':common:ui-widget'

if (!isKTVEmpty) {
    include ':scenes:ktv'
}

if (!isVoiceEmpty) {
    include ':scenes:voice'
    include ':scenes:voice:common'
}

if (!isSpatialVoiceEmpty) {
    include ':scenes:voice_spatial'
    include ':scenes:voice_spatial:common'
}

if (!isShowEmpty) {
    include ':scenes:show'
//    include ':scenes:show:aars:SenseArSourceManager'
//    include ':scenes:show:aars:STMobileJNI'

    /**
     * 引入美颜场景化 api, 美颜场景化项目根目录与声动互娱项目同级别，美颜场景化 api 需要做如下修改：
     * 1.注掉发布 maven 相关脚本代码
     * 2.rtc sdk 版本需与声动互娱项目版本一直，并需要把美颜场景画项目编译成功
     * 3.注意商汤美颜只引用了 lib_sensetime ，所以需要把对应的商汤美颜用的引用的aar都引用进来
     */
    def syncPath = new File(settingsDir, '../../BeautyAPI/Android/lib_sensetime')
    if (syncPath.exists()) {
        include ':lib_sensetime'
        project(':lib_sensetime').projectDir = syncPath

        include ':lib_sensetime_SenseArSourceManager'
        project(':lib_sensetime_SenseArSourceManager').projectDir = new File(settingsDir, '../../BeautyAPI/Android/lib_sensetime/libs/SenseArSourceManager')
        include ':lib_sensetime_STMobileJNI'
        project(':lib_sensetime_STMobileJNI').projectDir = new File(settingsDir, '../../BeautyAPI/Android/lib_sensetime/libs/STMobileJNI')
        include ':lib_sensetime_HardwareBuffer'
        project(':lib_sensetime_HardwareBuffer').projectDir = new File(settingsDir, '../../BeautyAPI/Android/lib_sensetime/libs/HardwareBuffer')
    }
}


